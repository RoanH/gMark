plugins{
	id 'eclipse'
	id 'application'
	id 'com.gradleup.shadow' version '8.3.2'
	id 'jacoco'
}

sourceSets{
	main.java.srcDirs = ['src']
	test.java.srcDirs = ['test']
}

dependencies{
	testImplementation(platform('org.junit:junit-bom:5.11.1'))
	testImplementation('org.junit.jupiter:junit-jupiter')
}

repositories{
	maven{
		allowInsecureProtocol = true
		url "$nexusPublic"
	}
}

version = findProperty("refName") ?: 'SNAPSHOT'
if(version.matches("v\\d+\\.\\d+")){
	version = version.substring(1)
}

eclipse.classpath.downloadSources = true
eclipse.classpath.downloadJavadoc = true
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
jacocoTestReport.reports.xml.required = true
sourceCompatibility = 1.17
javadoc.options.memberLevel = JavadocMemberLevel.PRIVATE
shadowJar.archiveBaseName = 'QuickSilver'
shadowJar.archiveVersion = 'v' + version
shadowJar.archiveClassifier = ''
application.mainClass = 'nl.group9.quicksilver.Main'

test{
	useJUnitPlatform()
	dependsOn 'cleanTest'

	testLogging{
		events "passed", "skipped", "failed"
	}
}

shadowJar{
	mergeServiceFiles()
}
